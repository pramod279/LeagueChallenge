plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion build_version.target_sdk
    buildToolsVersion build_version.build_tools_version

    defaultConfig {
        applicationId app_info.applicationID
        minSdkVersion build_version.min_sdk
        targetSdkVersion build_version.target_sdk
        versionCode app_info.version_code
        versionName app_info.version_name
        vectorDrawables.useSupportLibrary = false
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    /*Signed APK Configurations*/
    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        /*Debug Build Variant*/
        debug {
            applicationIdSuffix ".debug"
        }

        /*Release Build Variant*/
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.releaseConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    /*Default Flavor*/
    flavorDimensions "default"
    productFlavors {
        /*Development Flavor*/
        production {
            buildConfigField "String", "BASE_URL", server_config.league_host_url
            signingConfig signingConfigs.releaseConfig
        }
    }

    /*APK Name*/
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "$app_info.applicationName - v${variant.versionName}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    /*Android UI Support Libraries*/
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    /*Android Lifecycle Components*/
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.3.1"
    /*Retrofit Network Support Library*/
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'
    /*Android Room Database Components*/
    implementation "androidx.room:room-runtime:2.3.0"
    annotationProcessor "androidx.room:room-compiler:2.3.0"
    androidTestImplementation "androidx.room:room-testing:2.3.0"
    /*Glide Image Library*/
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    /*Shimmer Loader Library*/
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    /*Test Implementation*/
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    testImplementation "org.mockito:mockito-core:3.11.2"
    androidTestImplementation "org.mockito:mockito-android:3.11.2"
    androidTestImplementation("androidx.test:runner:1.4.0")
    androidTestImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
}